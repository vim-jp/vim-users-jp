---
layout: post
title: "Hack #213: ダブルクリックで単語検索するようにする"
date: 2011-04-16
author: tyru
---
<h2>きっかけ</h2>

<p>Vimユーザの方は一日を端末で過ごしている方も多いでしょう。
				しかしマウスとはとても便利なものです。
				トラックボールを使ったりすると新しい発見があるかもしれません。</p>

<p>キーボードで操作している時はキーボードで操作でき、
				マウスで操作している時はマウスで最低限の操作はできるというのが理想です。
				最低限の操作とは何でしょうか。
<!-- 編集するとしたらもちろんキーボードに手を伸ばさなければなりませんが -->
				結論から言いますと、あなたがマウスでコードを見ている時必要だと思うことは、大半が単語検索でしょう。</p>

<ul>
<li>離れた所にある同じ単語に飛ぶ</li>
<li>変数がどこで定義されているか調べる</li>
<li>検索目的ではなくバッファ中にあるテキストをハイライトさせたい</li>
</ul>

<p>など単語検索でできることは実に様々です。</p>

<p>自分は普通にコードを書いている時でも<code>/</code>コマンドをよく使います。
				移動の80%はこれといっていいかもしれません。
				言い過ぎだとしても70割かもしれません。
				それほど汎用的なコマンドなのです。</p>

<h2>(脱線) IDEの場合</h2>

<p>IDEだとしたら変数にマウスオーバーした時点で定義場所などの情報がポップアップで表示されるかもしれません。
				ただこういう機能は「便利すぎて」ユーザが能動的に情報を探そうとした場合邪魔になることが多いと思うのです。
				ちなみにマウスオーバーした時点でポップアップするようなIDEを筆者は知りません。勘で言いました。</p>

<p>自動補完にもこういう側面があると私は思います。
				キーをタイプしたらポップアップがめまぐるしく表示され、
				頭の中にあったコードがデリートされてしまうなどといったことがありました。</p>

<p>Twitterでチカチカしたアイコンをした人をフォローしてる人には身に覚えがあるかもしれませんが、
				気分転換にTwitterを見たらTLにアイコンがチカチカした人がいてとても気分を害されたということがあります。
				この対処法としてはtermtterなどの画像が表示されないTwitterクライアントを使うことです。
				ちなみにTwitter社はサードパーティ製クライアントを<a href="http://slashdot.jp/it/article.pl?sid=11/03/14/1012231">推奨していません</a>。</p>

<p>ちなみに筆者は以前自動補完はあまり好きではありませんでしたが慣れました。
				自動補完プラグインとしては<a href="http://www.vim.org/scripts/script.php?script_id=1879">autocomplpop</a>、<a href="https://github.com/Shougo/neocomplcache/">neocomplcache</a>というプラグインが代表的です。
				自動補完はとても便利な機能なのでぜひ使いましょう。</p>

<h2>閑話休題</h2>

<p>無理矢理話を戻すと、マウスでの単語検索を快適にするために必要な設定を本稿では解説したいと思います。</p>

<h2>ダブルクリックへのマッピング</h2>

<p>ダブルクリックへのマッピングは<code>&lt;2-LeftMouse&gt;</code>を使えばできます。</p>

<pre><code>nnoremap &lt;2-LeftMouse&gt; g*
</code></pre>

<p>これで終わりでしょうか？いいえ、<!-- こだまです -->まだです。</p>

<p>これだとダブルクリックをしようとした時、
				シングルクリック(<code>&lt;LeftMouse&gt;</code>)の時点で<code>&lt;LeftMouse&gt;</code>本来の操作、
				つまりクリックした場所にカーソルを移動、が実行されてしまうのです。
				カーソルが移動するのは多いに結構なのですが、
				問題は<code>'scrolloff'</code>の設定も効いてしまうことです。</p>

<p><code>'scrolloff'</code>は正の値をセットするとカーソルの上部か下部が
				常に<code>'scrolloff'</code>分の行だけ表示されるようになるという非常に便利なオプションですが、
				これがマウスのシングルクリックの時に効いてしまうと</p>

<ol>
<li>シングルクリック (クリック1回目)</li>
<li>カーソル移動</li>
<li>&#8216;scrolloff&#8217;分だけ行を開ける</li>
<li>表示領域がずれて、マウスカーソルが指していた場所にあった単語も移動してしまう</li>
<li>ダブルクリック (クリック2回目)</li>
<li>検索するつもりじゃなかった単語を検索してしまう</li>
<li>一瞬何が起こったのか分からない</li>
<li>理解し悲しみに暮れる</li>
</ol>

<p>ということになり非常に悲しいです。
				そして「9.Vimを<!-- 調教 --><!-- setq -->説得する」というのがこの記事を書くに至るまでの過程でした。
				ちなみに説得というのはEmacsで言うsetqとかけています。</p>

<p><!--
				少しダブルクリックまで待ってくれればいいのですが、
				`&lt;LeftMouse&gt;`と`&lt;2-LeftMouse&gt;`は違うマッピングなので
				`'timeout'`などのオプションは適用されないのです。
				-->
<!-- これはひどい。いやマジで -->
<!-- あれ？これ&lt;2-LeftMouseが&lt;LeftMouse&gt;&lt;LeftMouse&gt;だったらよかったんじゃね？ --></p>

<h2><code>'scrolloff'</code>の再実装 ～yak shaving～</h2>

<p>これを解決するには、つまりシングルクリック(<code>&lt;LeftMouse&gt;</code>)時の移動を抑制すればいい訳です。
				しかし<code>'scrolloff'</code>は設定しておきたい。
				ならば<code>'scrolloff'</code>がやることをVimスクリプトで再実装して制御可能にし、一時的に無効にすればいい訳です。
				以下がその<code>'scrolloff'</code>の実装です。
<code>'scrolloff'</code>オプションの代わりに<code>g:scrolloff</code>グローバル変数で指定するようになっています。</p>

<pre><code>set scrolloff=0
let g:scrolloff = 15

" Hack for &lt;LeftMouse&gt; not to adjust ('scrolloff') when single-clicking.
" Implement 'scrolloff' by auto-command to control the fire.
autocmd vimrc CursorMoved * call s:reinventing_scrolloff()
let s:last_lnum = -1
function! s:reinventing_scrolloff()
    if s:last_lnum &gt; 0 &amp;&amp; line('.') ==# s:last_lnum
        return
    endif
    let s:last_lnum = line('.')
    let winline     = winline()
    let winheight   = winheight(0)
    let middle      = winheight / 2
    let upside      = (winheight / winline) &gt;= 2
    " If upside is true, add winlines to above the cursor.
    " If upside is false, add winlines to under the cursor.
    if upside
        let up_num = g:scrolloff - winline + 1
        let up_num = winline + up_num &gt; middle ? middle - winline : up_num
        if up_num &gt; 0
            execute 'normal!' up_num."\&lt;C-y&gt;"
        endif
    else
        let down_num = g:scrolloff - (winheight - winline)
        let down_num = winline - down_num &lt; middle ? winline - middle : down_num
        if down_num &gt; 0
            execute 'normal!' down_num."\&lt;C-e&gt;"
        endif
    endif
endfunction
</code></pre>

<p>そしてこれは<code>:autocmd</code>を使って実装されているので
				以下のように<code>'eventignore'</code>オプションという
				Vimスクリプトを書いているような人しか知らないオプションを使って
				一時的に無効にできます。</p>

<pre><code>nnoremap &lt;silent&gt; &lt;LeftMouse&gt; &lt;Esc&gt;:set eventignore=all&lt;CR&gt;&lt;LeftMouse&gt;:set eventignore=&lt;CR&gt;
</code></pre>

<h2>visualstar.vim</h2>

<p>ここまででも十分ですが、マウスで選択し、クリックで検索できたらどんなに良い事でしょうか。
<a href="https://github.com/thinca/vim-visualstar">visualstar.vim</a>を使えばそれができます。</p>

<pre><code>vmap &lt;LeftMouse&gt; &lt;Plug&gt;(visualstar-g*)
</code></pre>

<h2>まとめ</h2>

<p>ここまでのまとめとして、以下を.vimrcに貼り付ければ完了です。</p>

<pre><code>set scrolloff=0
let g:scrolloff = 15

" Hack for &lt;LeftMouse&gt; not to adjust ('scrolloff') when single-clicking.
" Implement 'scrolloff' by auto-command to control the fire.
autocmd vimrc CursorMoved * call s:reinventing_scrolloff()
let s:last_lnum = -1
function! s:reinventing_scrolloff()
    if s:last_lnum &gt; 0 &amp;&amp; line('.') ==# s:last_lnum
        return
    endif
    let s:last_lnum = line('.')
    let winline     = winline()
    let winheight   = winheight(0)
    let middle      = winheight / 2
    let upside      = (winheight / winline) &gt;= 2
    " If upside is true, add winlines to above the cursor.
    " If upside is false, add winlines to under the cursor.
    if upside
        let up_num = g:scrolloff - winline + 1
        let up_num = winline + up_num &gt; middle ? middle - winline : up_num
        if up_num &gt; 0
            execute 'normal!' up_num."\&lt;C-y&gt;"
        endif
    else
        let down_num = g:scrolloff - (winheight - winline)
        let down_num = winline - down_num &lt; middle ? winline - middle : down_num
        if down_num &gt; 0
            execute 'normal!' down_num."\&lt;C-e&gt;"
        endif
    endif
endfunction

" Do not adjust current scroll position (do not fire 'scrolloff') on single-click.
nnoremap &lt;silent&gt; &lt;LeftMouse&gt;   &lt;Esc&gt;:set eventignore=all&lt;CR&gt;&lt;LeftMouse&gt;:set eventignore=&lt;CR&gt;
" Double-click for searching the word under the cursor.
nnoremap          &lt;2-LeftMouse&gt; g*
" Single-click for searching the word selected in visual-mode.
vmap              &lt;LeftMouse&gt; &lt;Plug&gt;(visualstar-g*)
</code></pre>

<!--
				キーボードだってマウスだって声だって使える。
				そう、Vimならね。
				-->

<address class="hack-author">tyru</address>
